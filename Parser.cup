import java_cup.runtime.*;
import java.io.*;
import java.util.*;


parser code {:
	   Lexer lexer;

	   public SmplParser(Lexer l) {
	   		  super(l);
			  lexer = l;
	   }

		public void report_error(String message, Object info) {
		    System.err.println(message);
		}

		public void syntax_error(Symbol cur_token) {
		    System.err.print("Line " + lexer.getLine() +
							 " near char " + lexer.getChar() + ": ");
		    report_error("Syntax error", cur_token);
		    System.err.println ("Last token read is " +
							   	 lexer.getText());
		}
:};

init with {:
	  :};

scan with {:
		try {
		    return lexer.next_token();
		} catch (java.io.IOException ioe) {
		    System.out.println("Line " + lexer.getLine() + ", pos " +
				       lexer.getChar() +
				       ": Unrecognised token");
		    System.out.println(ioe.getMessage());
		    throw ioe;
		}
	  :};



terminal LPAREN, RPAREN, LBRACE, RBRACE, LBRACKET, RBRACKET, LB_COLON, RB_COLON;
terminal IS_PAIR, PAIR, CAR, CDR, LIST, SIZE, EQV, EQL, SUBSTR, DEF, PROC, NIL;
terminal PRINT, PRINTLN;

// Arithmetic operators
terminal PLUS, MINUS, MUL, DIV, MOD;
terminal AND, OR, NOT;
terminal BIT_AND, BIT_OR, BIT_NOT;
terminal SEMI, AT;

// Comparators
terminal GT, LT, GE, LE, NE, ASSIGN, EQUAL, COMMA;

// Types
terminal String VAR;
terminal Integer INT;
terminal Double DOUBLE;
terminal Character CHAR;

terminal TRUE;
terminal FALSE;

non terminal ExpLit bool;

non terminal Exp expression, boolExp, term, factor;
non terminal Statement stmt;
non terminal StatementSequence stmtList;
non terminal StatementDefinition definition;
non terminal SmplProgram program;

// Function Def
non terminal StmtFuncDefinition funcDefn;
non terminal ArrayList<String> paramList;
non terminal ArrayList<String> paramListE;

// Function Call
non terminal ExpFuncCall funcCall;
non terminal ArrayList<Exp> argList;
non terminal ArrayList<Exp> argListE;

non terminal Exp list;
non terminal Exp pair;
non terminal Exp vector;
non terminal ArrayList<Exp> vectorBody;

non terminal ExpOps ops;

non terminal Exp print, println;

non terminal empty;

program ::= stmtList:seq {: RESULT = new SmplProgram(seq); :};

stmtList ::= stmtList:seq stmt:s {: RESULT = seq.add(s); :} | 
			 stmt:s {: RESULT = new StatementSequence(s); :};
			 
stmt ::= expression:e SEMI {: RESULT = new Statement(e); :} |
	 	 definition:d {: RESULT = d; :} |
		 funcDefn:f {: RESULT = f; :};


funcDefn ::= DEF VAR:var PROC LPAREN paramListE:pList RPAREN LBRACE stmtList:seq RBRACE
		 	 {: RESULT = new StmtFuncDefinition(var, pList, seq); :};

paramListE ::= paramList:pList {: RESULT = pList; :} |
		   	   empty {: RESULT = new ArrayList<String>(); :};

paramList ::= paramList:seq COMMA VAR:v  {: seq.add(v); RESULT = seq; :} |
		  	  VAR:v {:
					ArrayList<String> result = new ArrayList<>();
					result.add(v);
					RESULT = result;
			:};

definition ::= VAR:var ASSIGN expression:e SEMI {: RESULT = new StatementDefinition(var, e); :};

expression ::= expression:e PLUS  term:t {: RESULT = new ExpAdd(e, t); :} |
		   	   expression:e MINUS term:t {: RESULT = new ExpSub(e, t); :} |			   
			   boolExp:b {: RESULT = b; :};

// expression:e LBRACKET expression:i RBRACKET {: RESULT = new ExpOps("index", e, i); :} |

funcCall ::= VAR:v LPAREN argListE:aList RPAREN {: RESULT = new ExpFuncCall(v, aList); :};

argList ::= argList:seq COMMA expression:e  {: seq.add(e); RESULT = seq; :} |
			 	 expression:e {:
					ArrayList<Exp> result = new ArrayList<>();
					result.add(e);
					RESULT = result;
			:};
			
argListE ::= argList:aList {: RESULT = aList; :} |
		 	 empty {: RESULT = new ArrayList<Exp>(); :};

bool ::= TRUE {: RESULT = new ExpLit(true, "smpl-boolean"); :} |
	 	 FALSE {: RESULT = new ExpLit(false, "smpl-boolean"); :};

boolExp ::= boolExp:b AND term:t {: RESULT = new ExpAnd(b, t); :} |
			boolExp:b OR term:t {: RESULT = new ExpOr(b, t); :} |			
			term:t {: RESULT = t; :};

term ::= term:t MUL factor:f {: RESULT = new ExpMul(t, f); :} |
	 	 term:t DIV factor:f {: RESULT = new ExpDiv(t, f); :} |
		 term:t MOD factor:f {: RESULT = new ExpMod(t, f); :} |
		 factor:f {: RESULT = f; :};

factor ::= funcCall:f {: RESULT = f; :} |
	   	   INT:v {: RESULT = new ExpLit(v, "smpl-integer"); :} |
	   	   DOUBLE:v {: RESULT = new ExpLit(v, "smpl-double"); :} |
		   CHAR:c {: RESULT = new ExpLit(c, "smpl-character"); :} |
		   bool:b {: RESULT = b; :} |
		   pair:p {: RESULT = p; :} |
		   vector:v {: RESULT = v; :} |
		   list:l {: RESULT = l; :} |
		   ops:o {: RESULT = o; :} |
		   print:p {: RESULT = p; :} |
		   VAR:var {: RESULT = new ExpVar(var); :} |
		   LPAREN expression:e RPAREN {: RESULT = e; :} |		   
		   LPAREN MINUS expression:e RPAREN {: :};

print ::= PRINT LPAREN expression:exp RPAREN
	  	  {: RESULT = new ExpPrint(exp); :} |
		  PRINTLN LPAREN expression:exp RPAREN
	  	  {: RESULT = new ExpPrintln(exp); :};

ops ::= SIZE LPAREN expression:e RPAREN {: RESULT = new ExpOps("size", e); :} |
		CAR LPAREN expression:e RPAREN {: RESULT = new ExpOps("car", e); :} |
		CDR LPAREN expression:e RPAREN {: RESULT = new ExpOps("cdr", e); :} |
		EQV LPAREN expression:e1 COMMA expression:e2 RPAREN {: RESULT = new ExpOps("eqv", e1, e2); :} |
		EQL LPAREN expression:e1 COMMA expression:e2 RPAREN {: RESULT = new ExpOps("equ", e1, e2); :} |
		IS_PAIR LPAREN expression:e RPAREN {: RESULT = new ExpOps("is-pair", e); :};


list ::= LBRACKET argListE:aList RBRACKET
	   	 {: RESULT = new ExpLit(aList, "smpl-list"); :} |
		 LIST LPAREN argListE:aList RPAREN
		 {: RESULT = new ExpLit(aList, "smpl-list"); :};


vector ::= LB_COLON argListE:aList RB_COLON
	   	   {: RESULT = new ExpLit(aList, "smpl-vector"); :};


pair ::= PAIR LPAREN expression:e1 COMMA expression:e2 RPAREN
	 	 {:
			ArrayList<Exp> r = new ArrayList<Exp>();
			r.add(e1);
			r.add(e2);
			RESULT = new ExpLit(r, "smpl-pair");
		:};
		
empty ::= ;